#ifndef	__DECAL_PHL__
#define	__DECAL_PHL__

#include "sfx.h"
#include "scattering_light.h"
#include "decal.h"
#include "_pixel.h"

//--------------------------------------
// tire tracks & skid marks
//--------------------------------------

float4 ph_decal (VS_Out_Decal i) : COLOR0
{
	float	night			= IS_NIGHT;
	int		slice			= 0;
	float	shadow			= SHADOW_PXL_TRI( slice );
	float	bias			= SET_BIAS( slice );	// do this after shadow, b/c shadow sets slice
	float2	uv				= i.uvDeepTarmac.xy;

	float4	color;
			color.rgb		= i.diffuse.rgb;
			color.a			= tex2DB(sDiffuse, uv, bias).a;

	float4	normalSample	= tex2DB(sNormal, uv, bias);

	float4	tarmacColor;
			tarmacColor.rgb	= 0;
			tarmacColor.a	= normalSample.z;

	#ifdef	bCompression
	float3	normal;
			normal.xy		= normalSample.wy * 2 - 1;
			normal.z		= 1 - abs( normal.x - normal.y );
	#else
	float3	normal 			= normalSample.xyz * 2 - 1;
	#endif

//			normal			= normalize(normal);
			normal 			= mul(normal, i.TBNmatrix);
			normal 			= normalize(normal);


	// lighting prep
	float3	sunvec			= gDLight.Dir.xyz;

/*
	tire tracks didn't have slices implemented,
	so can't segment processing on things.
*/

	// do specular light
	float3	eyevec			= normalize(i.eyevec.xyz);
	float3	spccol			= i.diffuse.rgb * 0.3;
	float	spcpow			= 0.9;
	lights	light			= SetLightColors( shadow, spccol );


	float 	occlusion		= 1;

			// don't add this to tire tracks, or they spaz out / flicker
//			occlusion *= gDLight.Ambient.w;

	// AO spot under vehicles
	#ifdef	bOmni
			occlusion		*= GetOmniOcclusion( i.omni.xyz, bias );
			occlusion		= SaturateOcclusion ( occlusion, shadow );	// AO only occurs in shadows
	#endif

	float3	spcref			= reflect( -eyevec, normal );


	// finalize lighting
	float3	dots			= lightdots(normal, sunvec, spcref, spcpow, occlusion);
			light.sky		= ShadowHighlight( light.sky, dots, eyevec, normal );

	// do rain sheen after shadow highlight,
	// or shadow highlight negates sheen
	#if ( RAIN_SHEEN == 1 )
	float3	reflection		= GetCubeMap( spcref, bias );
	float	wetness			= dot( light.spc, 1 );
			light			= SetLightWetness( light, reflection, wetness, night );
	#endif

			light			= lightcols(  dots, light, shadow );
			color.rgb		= color.rgb * light.sun + light.spc;	// MAD


/*
	// everything's green ... so flag not set
	#if defined(bNoSlice1)
		color.r *= 10;
	#else
		color.g *= 10;
	#endif
*/
/*
	// everything's green ... so flag not set
	#if defined(bNoSlice2)
		color.r *= 10;
	#else
		color.g *= 10;
	#endif
*/
/*
	// everything's green ... so flag not set
	#if defined(bNoSlice3)
		color.r *= 10;	// slice 0, 1 turn red
	#else
		color.g *= 10;	// slice 2, 3 turn green
	#endif
*/


	// tarmac color is black skid mark color on asphalt
	// so, do lighting on color first before merging with tarmac color
	// in order to keep black skid marks black

			color			= lerp(color, tarmacColor, i.uvDeepTarmac.w);
			color.a			*= i.diffuse.a;		// mul this once after lerp'ing instead of twice to each before lerp
			color.rgb		*= i.extinction.rgb;
			color.rgb		+= NIGHT_DARKEN2(i.inscatter.rgb, night);
	return	color;
}

#endif